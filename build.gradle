/*
 * Copyright, 2019, CleverDATA, LLC.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import groovy.io.FileType

buildscript {
    apply from: 'libraries.gradle'
    apply from: 'repositories.gradle'
    repositories repos.resolve.all
}

ext {
    meta = [
        buildBy: System.getProperty('user.name'),
        createdBy: "${System.getProperty('java.version')} (${System.getProperty('java.specification.vendor')})",
        orgName: 'CleverData Inc.',
        orgUrl: 'http://www.cleverdata.ru',
        projectGroup: 'io.dmpkit',
    ]
}

apply plugin: 'groovy'
apply plugin: 'java-gradle-plugin' // make gradle's testkit happy
apply plugin: 'maven-publish'
apply plugin: 'idea'
apply plugin: 'jacoco'
apply plugin: 'signing'

repositories repos.resolve.all
dependencies {
    implementation libs.external.gradleApi
    implementation libs.external.gradleGroovy
    implementation libs.external.grgit

    testImplementation libs.external.hamcrest
    testImplementation libs.external.testng

    jacocoAnt libs.external.jacoco
    jacocoAgent libs.external.jacocoAgent
}

// apply plugin to itself
def scriptEngine = new GroovyScriptEngine(
    sourceSets.main.allSource.srcDirs as String[],
    new URLClassLoader(configurations.runtimeClasspath.collect { it.toURI().toURL() } as URL[], buildscript.classLoader)
)
sourceSets.main.allSource.srcDirs.each { File srcDir ->
    if (srcDir.exists()) {
        srcDir.traverse(type: FileType.FILES, nameFilter: ~/.*Plugin\.groovy/) {
            String pluginPath = srcDir.toPath().relativize(it.toPath()).toString()
            apply plugin: scriptEngine.loadScriptByName(pluginPath)
        }
    }
}

idea {
    project {
        jdkName = '1.8'
        languageLevel = '1.8'
        vcs = 'Git'
    }
    module {
        name = project.name
    }
}

group meta.projectGroup
description 'DMPKIT :: GRADLE :: PLUGIN :: RELEASE'

defaultTasks 'build'
buildDir 'target'

model {
    publishing {
        publications {
            pluginMaven {
                artifact sourceJar {
                    classifier 'sources'
                }
                pom {
                    name = 'DMPkit Gradle Release Plugin'
                    description = 'DMPkit Gradle Release Plugin provides additional tasks to release git projects easier'
                    url = 'https://github.com/cleverdata/dmpkit-gradle-release-plugin'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'mpryahin'
                            name = 'Mike Pryakhin'
                        }
                        developer {
                            id = 'kent2171'
                            name = 'Rinat Sharipov'
                        }
                        developer {
                            id = 'szhem'
                            name = 'Sergey Zhemzhitsky'
                        }
                    }
                    organization {
                        name = 'CleverDATA'
                        url = 'http://cleverdata.ru/en'
                    }
                    scm {
                        connection = 'scm:git:git@github.com:cleverdata/dmpkit-gradle-release-plugin.git'
                        developerConnection = 'scm:git:ssh://git@github.com:cleverdata/dmpkit-gradle-release-plugin.git'
                        url = 'https://github.com/cleverdata/dmpkit-gradle-release-plugin'
                    }
                    issueManagement {
                        system = 'GitHub'
                        url = 'https://github.com/cleverdata/dmpkit-gradle-release-plugin/issues'
                    }
                    distributionManagement {
                        downloadUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
                    }
                    packaging = 'jar'
                }
            }
        }
        signing {
            sign publications
        }
        repositories repos.publish.smart
    }
}

tasks.withType(GroovyCompile) {
    options.encoding = 'UTF-8'
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

task sourceJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allSource
}
artifacts.archives sourceJar

configurations {
    itestImplementation.extendsFrom testImplementation
}

sourceSets {
    itest
}

test {
    useTestNG()
    testLogging {
        showCauses true
        showExceptions true
        showStackTraces true
        showStandardStreams true
    }
    include '**/*Test.class'
}

task itest(type: Test) {
    description 'Runs the integration tests'
    useTestNG()
    testLogging {
        showCauses true
        showExceptions true
        showStackTraces true
        showStandardStreams true
    }

    include '**/*IT.class'
    testClassesDirs = sourceSets.itest.output.classesDirs
    classpath = sourceSets.itest.runtimeClasspath
}

jacocoTestReport {
    reports {
        html.enabled true
        xml.enabled false
        csv.enabled false
    }
}
jacocoTestReport.dependsOn test, itest
check.dependsOn jacocoTestReport

jar.doFirst {
    manifest {
        attributes (
            'Created-By': meta.createdBy,
            'Built-By': meta.buildBy,

            'Build-Branch': dmpkitRelease.currentBranch(),
            'Build-Revision': dmpkitRelease.revision(),
            'Build-Timestamp': System.currentTimeMillis(),

            'Implementation-Title': project.description,
            'Implementation-Vendor': meta.orgName,
            'Implementation-Vendor-Id': project.group,
            'Implementation-Version': project.version,
            'Implementation-URL': meta.orgUrl,

            'Specification-Title': project.description,
            'Specification-Version': project.version,
            'Specification-Vendor': meta.orgName
        )
    }
}

gradlePlugin {
    plugins {
        // using dmpkitGradleRelease instead of dmpkitRelease as we are applying this plugin to itself
        dmpkitGradleRelease {
            id = 'dmpkit-gradle-release'
            implementationClass = 'io.dmpkit.gradle.plugin.release.ReleasePlugin'
        }
    }
    testSourceSets sourceSets.itest
}

dmpkitRelease {
    envBuildNumber = 'BUILD_NUMBER'
    envDefaultBranch = 'BRANCH_NAME'
}

// workaround for
// - https://github.com/gradle/gradle/issues/10363
// - https://github.com/gradle/gradle/pull/10366
gradle.taskGraph.whenReady { taskGraph ->
    taskGraph.allTasks
        .findAll { it instanceof Sign }
        .each { task ->
            task.doFirst {
                def gpgDir = mkdir "${buildDir}/gnupg"
                file("${gpgDir}/secring.gpg").with {
                    deleteOnExit()
                    bytes = project.findProperty("secretKeyRing").decodeBase64()
                }
                project.ext."signing.keyId" = project.findProperty("secretKeyId")
                project.ext."signing.secretKeyRingFile" = "${gpgDir}/secring.gpg"
                project.ext."signing.password" = project.findProperty("signingPassword")
            }
        }
}
